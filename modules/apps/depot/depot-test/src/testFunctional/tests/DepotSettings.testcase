@component-name = "portal-document-management"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Depot";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONDepot.deleteDepot(depotName = "Test Depot Name");

			Site.tearDownCP();

			DMDocument.tearDownCP();

			DMFolder.tearDownCP();

			User.logoutPG();
		}
	}

	@description = "This test covers LPS-127369. This ensures that the applications in a depot can be disabled and enabled."
	@priority = "4"
	@refactorneeded
	test DisableAndEnableApplications {
		JSONDepot.addDepot(
			depotDescription = "This is the description of a depot",
			depotName = "Test Depot Name");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoDepotPortlet(
			depotName = "Test Depot Name",
			portlet = "Settings");

		Depot.configureApplication(
			applicationList = "Documents and Media,Collections,Translation Processes,Web Content",
			status = "Disabled");

		DepotNavigator.openDepotDashboard(depotName = "Test Depot Name");

		for (var portletName : list "Web Content,Documents and Media,Collections,Translation Processes") {
			AssertElementNotPresent(
				key_card = "${portletName}",
				locator1 = "Card#CARD_TITLE");
		}

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoDepotPortlet(
			depotName = "Test Depot Name",
			portlet = "Settings");

		Depot.configureApplication(
			applicationList = "Documents and Media,Collections,Translation Processes,Web Content",
			status = "Enabled");

		DepotNavigator.openDepotDashboard(depotName = "Test Depot Name");

		for (var portletName : list "Web Content,Documents and Media,Collections,Translation Processes") {
			AssertTextEquals(
				key_card = "${portletName}",
				locator1 = "Card#CARD_TITLE",
				value1 = "${portletName}");
		}
	}

	@description = "This ensures that a depot can be edited via settings."
	@priority = "4"
	@refactorneeded
	test EditName {
		JSONDepot.addDepot(
			depotDescription = "This is the description of a depot",
			depotName = "Test Depot For Edit Name");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoDepotPortlet(
			depotName = "Test Depot For Edit Name",
			portlet = "Settings");

		Depot.editDepot(
			depotDescriptionEdit = "This is an edited description of a depot",
			depotName = "Test Depot For Edit Name",
			depotNameEdit = "Test Depot For Edited Name",
			settings = "true");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoDepot(depotName = "Test Depot For Edited Name");
	}

	@description = "This ensures that a user can set the depot site connection via settings."
	@priority = "4"
	@refactorneeded
	@uitest
	test SearchForDocument {
		property test.name.skip.portal.instance = "DepotSettings#SearchForDocument";

		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Page Name");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Site Name",
			layoutName = "Page Name",
			widgetName = "Search Bar");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Site Name",
			layoutName = "Page Name",
			widgetName = "Search Results");

		JSONDepot.addDepot(
			depotDescription = "This is the description of a depot",
			depotName = "Test Depot Name");

		JSONDocument.addFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			groupName = "Test Depot Name");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoDepotPortlet(
			depotName = "Test Depot Name",
			portlet = "Settings");

		AssertElementNotPresent(locator1 = "Button#BACK");

		DepotNavigator.gotoSites(depotName = "Test Depot Name");

		Depot.connectSite(siteList = "Site Name");

		Navigator.gotoSitePage(
			pageName = "Page Name",
			siteName = "Site Name");

		Search.searchCP(searchTerm = "DM Document Title");

		SearchResultPortlet.viewSearchResults(
			searchAssetTitle = "DM Document Title",
			searchAssetType = "Document");

		SearchPortlets.gotoResultDetails(searchAssetTitle = "DM Document Title");

		SearchPortlets.viewResultDetails(searchAssetTitle = "DM Document Title");
	}

	@description = "This ensures that the depot language can be set via settings."
	@priority = "4"
	@refactorneeded
	test ViewCustomLanguage {
		JSONDepot.addDepot(
			depotDescription = "This is the description of a depot",
			depotName = "Test Depot Name");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoEdit(depotName = "Test Depot Name");

		DepotNavigator.openDepotLanguages(depotName = "Test Depot Name");

		Click.clickNoMouseOverNoWaitForVisible(
			key_languagesOption = "Define a custom default language and additional active languages for this asset library.",
			locator1 = "Radio#DEPOT_LANGUAGES_OPTION");

		Depot.configureDefaultLanguage(
			defaultDepotLanguage = "Spanish (Spain)",
			locale = "es_ES");

		DepotNavigator.openDepotAdmin();

		DepotNavigator.gotoDepotPortlet(
			depotName = "Test Depot Name",
			portlet = "Settings");

		AssertElementNotPresent(locator1 = "Button#BACK");

		Depot.viewGeneralLanguageOptions(currentLanguageList = "es_ES");

		AssertElementPresent(
			key_unnamedTitle = "Repositorio sin nombre",
			locator1 = "TextInput#DEPOT_UNNAMED_DEFAULT_TITLE");

		DepotNavigator.openDepotLanguages(depotName = "Test Depot Name");

		Depot.viewDefaultLanguage(defaultDepotLanguage = "Spanish (Spain)");
	}

}