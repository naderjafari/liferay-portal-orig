@component-name = "portal-collaboration"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Message Boards";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			MessageboardsCategory.tearDownCP();

			MessageboardsThread.tearDownCP();

			MessageboardsPortlet.tearDownConfigurationGeneralCP();
		}
	}

	@priority = "5"
	@refactordone
	@uitest
	test CanAddMBCategory {
		property portal.acceptance = "true";

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsCategory.addCP(categoryName = "MB Category Name");

		MessageboardsCategory.viewCP(
			categoryCount = "0 Categories",
			categoryName = "MB Category Name",
			threadCount = "0");
	}

	@priority = "4"
	@refactordone
	test CanAddMBCategorySubcategory {
		JSONMBMessage.addCategory(
			categoryDescription = "MB Category Description",
			categoryName = "MB Category Name",
			userEmailAddress = "test@liferay.com");

		var parentCategoryId = JSONMBMessage.getCategoryIdByName(
			categoryName = "MB Category Name",
			groupName = "Guest");

		JSONMBMessage.addCategory(
			categoryDescription = "MB Subcategory Description",
			categoryName = "MB Subcategory Name",
			groupName = "Guest",
			parentCategoryId = "${parentCategoryId}",
			userEmailAddress = "test@liferay.com");

		MessageboardsCategory.openToCategory(
			categoryName = "MB Category Name",
			groupName = "Guest",
			siteURLKey = "guest");

		MessageboardsCategory.viewCP(
			categoryCount = "0 Categories",
			categoryName = "MB Subcategory Name",
			threadCount = "0");
	}

	@priority = "5"
	@refactordone
	@uitest
	test CanAddMBThread {
		property app.server.types = "jboss,tcserver,tomcat,weblogic,websphere,wildfly";
		property database.types = "db2,hypersonic,mariadb,mysql,oracle,postgresql,sqlserver,sybase";
		property environment.acceptance = "true";
		property portal.acceptance = "true";

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsThread.addCP(
			threadBody = "MB Thread Message Body",
			threadSubject = "MB Thread Message Subject");

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsThread.viewCP(
			replyCount = "0",
			threadBody = "MB Thread Message Body",
			threadSubject = "MB Thread Message Subject");
	}

	@priority = "4"
	@refactordone
	test CanDeleteMBCategories {
		for (var num : list "1,2,3") {
			JSONMBMessage.addCategory(
				categoryDescription = "MB Category Description",
				categoryName = "MB Category${num} Name",
				userEmailAddress = "test@liferay.com");
		}

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsCategory.deleteCP();

		MessageboardsCategory.viewDefault();
	}

	@priority = "3"
	@refactordone
	test CanDeleteMBThreadWithAttachment {
		property custom.properties = "trash.entry.check.interval=1${line.separator}trash.entries.max.age=1";

		JSONGroup.addGroup(groupName = "Site Name");

		JSONMBMessage.addMessage(
			groupName = "Site Name",
			threadBody = "Message Boards Thread Body",
			threadSubject = "MB Thread Message Subject");

		JSONMBMessage.addAttachmentToMessage(
			fileName = "Document_1.jpg",
			groupName = "Site Name",
			mimeType = "image/jpeg",
			threadSubject = "MB Thread Message Subject");

		JSONMBMessage.moveThreadToTrash(
			groupName = "Site Name",
			threadSubject = "MB Thread Message Subject");

		AssertConsoleTextNotPresent(value1 = "MessageListenerException");

		AssertConsoleTextNotPresent(value1 = "com.liferay.portlet.documentlibrary.NoSuchFileEntryException");

		RecycleBin.openRecycleBinAdmin(siteURLKey = "site-name");

		RecycleBin.deleteCP(
			assetName = "MB Thread Message Subject",
			assetType = "Message Boards Thread");

		RecycleBin.viewDefaultCP(assetName = "MB Thread Message Subject");
	}

	@priority = "5"
	@refactordone
	test CanEditMBThreadSubjectAndBody {
		JSONMBMessage.addMessage(
			groupName = "Guest",
			threadBody = "MB Thread Message Body",
			threadSubject = "MB Thread Message Subject");

		JSONMBMessage.updateMessage(
			groupName = "Guest",
			threadBodyEdit = "MB Thread Message Body Edit",
			threadSubject = "MB Thread Message Subject",
			threadSubjectEdit = "MB Thread Message Subject Edit");

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsThread.viewCP(
			replyCount = "0",
			threadBody = "MB Thread Message Body Edit",
			threadSubject = "MB Thread Message Subject Edit");
	}

	@priority = "3"
	@refactordone
	test CanViewAnonymousPostingAllowedCheckboxConfiguration {
		property test.name.skip.portal.instance = "CPMessageboards#ViewAnonymousPostingAllowedCheckboxConfiguration";

		MessageboardsThread.viewAnonymousPostingCheckboxCP(siteURLKey = "guest");

		MessageboardsPortlet.configureAnonymousPostingCP(
			anonymousPostingEnabled = "false",
			siteURLKey = "guest");

		MessageboardsPortlet.viewAnonymousPostingConfigurationCP(
			anonymousPostingEnabled = "false",
			siteURLKey = "guest");

		MessageboardsThread.viewNoAnonymousPostingCheckboxCP(siteURLKey = "guest");
	}

	@priority = "4"
	@refactordone
	test CanViewBBCodeNoXSS {
		MessageboardsPortlet.configureGeneralCP(siteURLKey = "guest");

		MessageboardsThread.addCP(
			siteURLKey = "guest",
			source = "true",
			threadBody = "[img]Not An Image[font= onerror=alert(/XSS/.source)//]FF[/font][/img]",
			threadSubject = "New Thread Subject");

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsThread.assertAlertNotPresent();

		MessageboardsThread.viewCP(
			replyCount = "0",
			threadBody = "",
			threadSubject = "New Thread Subject");

		MessageboardsThread.addCP(
			fontSize = "24",
			siteURLKey = "guest",
			source = "true",
			threadBody = "[size=5]Thread Body[/size]",
			threadSubject = "New Thread Subject 2");

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsThread.viewCP(
			fontSize = "24",
			replyCount = "0",
			threadBody = "Thread Body",
			threadSubject = "New Thread Subject 2");
	}

}